### Configuration variables ###

:global ddnsapi "https://1v9rrx0784.execute-api.eu-west-2.amazonaws.com/v1/ddns/"
:global ddnsuser "USERNAME"
:global ddnspasswd "PASSWORD"

:global ddnsrecord "www"
:global ddnsdomain "jtnet.co.uk"

:global extint "pppoe-out1"

### ### ### ### ### ### ### ###


# Define Global Variables
:global ddnsip
:global ddnslastip
:if ([ :typeof $ddnslastip ] = nil ) do={ :global ddnslastip "0" }

# Log
:log debug ("DDNS: Running script")
:log debug ("DDNS: External IP last time " . $ddnslastip )

# Get the current IP address on the external interface.
:global ddnsip [ /ip address get [/ip address find interface=$extint ] address ]
:log debug ("DDNS: External IP is currently " . $ddnsip)

# Did we get an IP address to compare
:if ([ :typeof $ddnsip ] = nil ) do={
   :log error ("DDNS: No ip address present on pppoe-out1, please check.")
} else={
  :if ($ddnsip != $ddnslastip) do={
    :local ip [:pick $ddnsip 0 [:find $ddnsip "/"]]
    :log info ("DDNS: Sending update for " . $ddnsrecord . "." . $ddnsdomain . " to " . $ip)
    :local url ($ddnsapi . $ddnsdomain . "/" . $ddnsrecord . "\?type=A&value=" .  $ip)
    :log debug ("DDNS URL: " $url)
    /tool fetch url="$url" mode=https user=$ddnsuser password=$ddnspasswd dst-path=("/DDNS." . $ddnsrecord . "." . $ddnsdomain . ".json")
    :global ddnslastip $ddnsip
    :log debug ("DDNS: Last IP now: ". $ddnslastip)
  } else={
    :log debug ("DDNS: No change in IP since last time. Doing nothing.")
  }
}